<part>          = <ows> events? <ows>
<events>        = event (<ws> event)*
<events-inline> = event (<ws-inline> event)*

    (* notes, chords & other events *)

<event>         = single-event | repeat | voices
<single-event>  = set-variable /
                  (
                    chord | note | rest | octave-change |
                    clj-expr-cached | marker | at-marker | barline |
                    event-sequence | cram | get-variable
                  )

repeat          = single-event <ows> <"*"> <ows> positive-number

    (* chords, notes, rests *)

chord           = (note | rest) <ows> subchord+
<subchord>      = <ows> <"/"> <ows> (octave-change | clj-expr-cached)*
                  <ows> (note | rest)
note            = pitch duration? (<ows> slur)?
rest            = <"r"> duration?

    (* pitch *)

pitch           = #"[a-g]" accidental*
<accidental>    = flat | sharp | natural
flat            = "-"
sharp           = "+"
natural         = "="

    (* octaves *)

<octave-change> = (octave-set | octave-up | octave-down)
octave-set      = <"o"> number
octave-up       = <">">
octave-down     = <"<">

    (* markers *)

marker          = <"%"> name
at-marker       = <"@"> name

